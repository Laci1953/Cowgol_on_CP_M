var infcb: FCB;

sub I_b8(): (b: uint8) is
	b := FCBGetChar(&infcb);
end sub;

sub I_b16(): (b: uint16) is
	b := I_b8() as uint16;
	b := b | (I_b8() as uint16 << 8);
end sub;

sub I_b32(): (b: uint32) is
	b := I_b16() as uint32;
	b := b | (I_b16() as uint32 << 16);
end sub;

sub I_bsize(): (s: Size) is
	s := I_b16() as Size;
end sub;

sub I_countedstring(): (s: string) is
	var len := I_b8();
	s := Alloc((len + 1) as intptr);
	var p := s;
	while len != 0 loop
		[p] := I_b8();
		p := p + 1;
		len := len - 1;
	end loop;
	[p] := 0;
end sub;

sub InputterOpenfile(filename: string) is
	if FCBOpenIn(&infcb, filename) != 0 then
		CannotOpen(filename);
	end if;
end sub;

sub InputterClosefile() is
	var e := FCBClose(&infcb);
end sub;


